plugins {
    id "org.jetbrains.intellij" version "${intellijPluginVersion}"
    id "idea"
}

runIde {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

runIdeForUiTests {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

intellij {
    version = ideaVersion //for a full list of Jetbrains IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    pluginName = 'Telemetry by Red Hat'
    updateSinceUntilBuild = false
}

patchPluginXml {
    sinceBuild = sinceIdeaBuild
    version = projectVersion
}

runPluginVerifier {
    ideVersions = [ideaVersion]
}

publishPlugin {
    token = jetBrainsToken
    channels = [jetBrainsChannel]
}

dependencies {
    implementation(
            'com.segment.analytics.java:analytics:2.1.1',
            'com.squareup.okhttp3:okhttp:4.9.3'
    )
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.8.2',
            "org.assertj:assertj-core:3.22.0",
            "org.mockito:mockito-inline:4.5.1"
    )
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
    afterSuite { desc, result ->
        if (!desc.parent)
            println("${result.resultType} " +
                    "(${result.testCount} tests, " +
                    "${result.successfulTestCount} successes, " +
                    "${result.failedTestCount} failures, " +
                    "${result.skippedTestCount} skipped)")
    }
}

configurations {
    implementation {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

java {
    withSourcesJar()
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://repository.jboss.org' }
}

group 'com.redhat.devtools.intellij'
version projectVersion

